configurations { antjar }
repositories { mavenCentral() }
dependencies { antjar 'org.apache.ant:ant:1.10.9' }

def antBuildDir = new File(buildDir, 'ant')
ant.properties.build = antBuildDir
ant.properties.jardir = buildDir
ant.importBuild 'build.xml'

task findAntJar(dependsOn: configurations.antjar) {
    doFirst {
        for (jar in configurations.antjar.resolve()) {
            if (jar.name =~ '^ant-\\d') { // need main jar, not launcher
                ant.properties.antjar = jar
            }
        }
    }
}

tasks['prepare-build'].dependsOn findAntJar

allprojects {
    version = '1.0'

    if (project != rootProject) {
        buildDir = new File(rootProject.buildDir, name)
        task sourcesJar(type: Jar) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            archiveClassifier = 'sources'
            from(projectDir) {
                include '*.java'
                include '*.yeti'
                into (project.name == 'yeti-lib' ? 'yeti/lang' :
                        'yeti/lang/compiler')
            }
            if (project.name == 'yeti-lib') {
                from(rootProject.file('modules')) {
                    include '*.yeti'
                }
            }
        }
        task packageJavadoc(type: Jar, dependsOn: rootProject.javadoc) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            archiveClassifier = 'javadoc'
            from new File(new File(antBuildDir, 'javadoc'), projectDir.name)
        }
    }

    if (name == 'yeti-compiler') {
        task jar(type: Jar, dependsOn: rootProject.jar) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            from(zipTree(new File(rootProject.buildDir, "yeti-${version}.jar"))) {
                exclude 'yeti/*.class'
                exclude 'yeti/lang/*.class'
            }
            manifest.attributes("Class-Path": "yeti-lib.jar",
                                "Main-Class": "yeti.lang.compiler.yeti")
        }
    }
}

ant.properties['yeti.jar'] = new File(buildDir, "yeti-${version}.jar")

clean {
    delete buildDir
}

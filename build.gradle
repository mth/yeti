configurations { antjar }
repositories { mavenCentral() }
dependencies { antjar 'org.apache.ant:ant:1.10.9' }

def antBuildDir = new File(buildDir, 'ant')
ant.properties.build = antBuildDir
ant.properties.jardir = buildDir
ant.importBuild 'build.xml'

task findAntJar(dependsOn: configurations.antjar) {
    doFirst {
        for (jar in configurations.antjar.resolve()) {
            if (jar.name =~ '^ant-\\d') { // need main jar, not launcher
                ant.properties.antjar = jar
            }
        }
    }
}

tasks['prepare-build'].dependsOn findAntJar

allprojects {
    apply plugin: 'maven-publish'

    version = '1.0'

    if (project != rootProject) {
        buildDir = new File(rootProject.buildDir, name)
        task sourcesJar(type: Jar) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            classifier = 'sources'
            from(projectDir) {
                include '*.java'
                include '*.yeti'
                into (project.name == 'yeti-lib' ? 'yeti/lang' :
                        'yeti/lang/compiler')
            }
            if (project.name == 'yeti-lib') {
                from(rootProject.file('modules')) {
                    include '*.yeti'
                }
            }
        }
        task javadocJar(type: Jar, dependsOn: rootProject.javadoc) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            classifier = 'javadoc'
            from new File(new File(antBuildDir, 'javadoc'), projectDir.name)
        }
    }

    rootProject.ant.properties["${name}.jar"] =
        new File(buildDir, "$name-${version}.jar")
    def buildJar = rootProject.jar
    if (name == 'yeti-compiler') {
        task jar(type: Jar, dependsOn: rootProject.jar) {
            destinationDirectory = buildDir
            archiveBaseName = project.name
            archiveVersion = project.version
            from(zipTree(new File(rootProject.buildDir,
                                  "yeti-${project.version}.jar"))) {
                exclude 'yeti/*.class'
                exclude 'yeti/lang/*.class'
            }
            manifest.attributes("Class-Path": "yeti-lib.jar",
                                "Main-Class": "yeti.lang.compiler.yeti")
        }
        buildJar = jar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'io.github.mth.yeti'
                artifactId = project.name
                version = project.version
                artifact(new File(buildDir, "$project.name-${version}.jar")) {
                    builtBy buildJar
                }
                if (project != rootProject) {
                    artifact sourcesJar
                    artifact javadocJar
                }
                pom {
                    url = 'https://mth.github.io/yeti/'
                    licenses {
                        license {
                            name = 'Simplified BSD License'
                            url = 'https://mth.github.io/yeti/LICENSE.txt'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/mth/yeti.git'
                        developerConnection = 'scm:git:ssh:git@github.com:mth/yeti.git'
                        url = 'https://github.com/mth/yeti/'
                    }
                }
            }
        }
    }
}

clean { delete buildDir }

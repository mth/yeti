configurations { antjar }
repositories { mavenCentral() }
dependencies { antjar 'org.apache.ant:ant:1.10.9' }

ant.properties.build = buildDir
ant.properties.jardir = buildDir
ant.importBuild 'build.xml'

task findAntJar(dependsOn: configurations.antjar) {
    doFirst {
        for (jar in configurations.antjar.resolve()) {
            if (jar.name =~ '^ant-\\d') { // need main jar, not launcher
                ant.properties.antjar = jar
            }
        }
    }
}

tasks['prepare-build'].dependsOn findAntJar

allprojects {
    version = '1.0'

    if (project != rootProject) {
        buildDir = new File(rootProject.buildDir, name)
    }

    if (name == 'yeti-compiler') {
        task jar(type: Jar, dependsOn: rootProject.jar) {
            destinationDirectory = buildDir
            archiveBaseName = "yeti-compiler"
            archiveVersion = project.version
            from(zipTree(new File(rootProject.buildDir, "yeti-${version}.jar"))) {
                exclude 'yeti/*.class'
                exclude 'yeti/lang/*.class'
            }
            manifest.attributes("Class-Path": "yeti-lib.jar",
                                "Main-Class": "yeti.lang.compiler.yeti")
        }
    }
}

ant.properties['yeti.jar'] = new File(buildDir, "yeti-${version}.jar")
